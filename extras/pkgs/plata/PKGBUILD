# Maintainer: nightuser <nightuser.android@gmail.com>

pkgname=plata-theme
pkgver=0.9.9
pkgrel=1
pkgdesc='A Gtk+ theme based on Material Design Refresh'
arch=('any')
url='https://gitlab.com/tista500/plata-theme'
license=('CCPL' 'GPL2')
makedepends=('git' 'inkscape' 'libxml2' 'parallel' 'sassc' 'zip')
optdepends=('gtk-engine-murrine: for gtk2 themes'
            'ttf-roboto: Recommended font'
            'marco: Mate support, a package rebuilt is required - community/mate-common')
source=("git+https://gitlab.com/tista500/plata-theme.git#tag=${pkgver}"
        "chromium_style.patch")
sha256sums=('SKIP'
            '32132c025e9654c257c84e3e2a9aa907604d03a97f5317a0c3c47ca39fbb755a')

build() {
  # Dirty hack for aur:
  # mate support will be built only if marco is present
  if LC_ALL=C pacman -Qq marco > /dev/null 2>&1
  then
    MARCO_FLAGS=""
  else
    MARCO_FLAGS="--disable-mate"
  fi

  cd "$pkgname"

  c_select='#1565c0'
  c_accent='#ebdbb2'
  c_suggest='#01579b'
  c_destruct='#f44336'

  ./autogen.sh \
    --prefix='/usr' \
    --enable-parallel \
    --enable-gtk_next \
    --with-selection_color="$c_select" \
    --with-accent_color="$c_accent" \
    --with-suggestion_color="$c_suggest" \
    --with-destruction_color="$c_destruct" \
    "$MARCO_FLAGS"

  # patches
  echo "Applying patchs"
  git apply --verbose "$srcdir/chromium_style.patch"

  # replace_colors
  echo "Replacing outstatning colors"
  find . -type f -exec sed -i -- "
    s/#3f51b5/$c_select/g
    s/#7986cb/$c_accent/g
    s/#673ab7/$c_suggest/g
    s/#f44336/$c_destruct/g
  " {} +

  make -j1 # it uses GNU Parallel instead
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE_CC_BY_SA4 -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}

# vim: ts=2 sw=2 et:
