# Maintainer: nightuser <nightuser.android@gmail.com>

pkgname=plata-theme
pkgver=0.9.9
pkgrel=1
pkgdesc='A Gtk+ theme based on Material Design Refresh'
arch=('any')
url='https://gitlab.com/tista500/plata-theme'
license=('CCPL' 'GPL2')
makedepends=('git' 'inkscape' 'libxml2' 'parallel' 'sassc' 'zip')
optdepends=('gtk-engine-murrine: for gtk2 themes'
            'ttf-roboto: Recommended font'
            'marco: Mate support, a package rebuilt is required - community/mate-common')
source=("git+https://gitlab.com/tista500/plata-theme.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  # Dirty hack for aur:
  # mate support will be built only if marco is present
  if LC_ALL=C pacman -Qq marco > /dev/null 2>&1
  then
    MARCO_FLAGS=""
  else
    MARCO_FLAGS="--disable-mate"
  fi

  cd "$pkgname"

  ./autogen.sh \
    --prefix='/usr' \
    --enable-parallel \
    --enable-gtk_next \
    --with-selection_color='#1565c0' \
    --with-accent_color='#2196f3' \
    --with-suggestion_color='#01579b' \
    --with-destruction_color='#f44336' \
    "$MARCO_FLAGS"

  # replace_colors
  echo "Replacing outstatning colors"
  find . -type f -exec sed -i -- '
    s/#3f51b5/#1565c0/g
    s/#7986cb/#2196f3/g
    s/#673ab7/#01579b/g
    s/#f44336/#f44336/g
  ' {} +

  make -j1 # it uses GNU Parallel instead
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE_CC_BY_SA4 -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}

# vim: ts=2 sw=2 et:
